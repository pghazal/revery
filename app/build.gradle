apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.pghaz.revery"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 13
        versionName "1.7.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release_signing_config {
            storeFile file('../misc/revery.keystore')
            keyPassword properties.getProperty('KEY_PASSWORD')
            storePassword properties.getProperty('STORE_PASSWORD')
            keyAlias 'revery'
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true

            // Make sure this is consistent with the redirect URI scheme used in debug/strings.xml
            manifestPlaceholders = [
                    'appAuthRedirectScheme': 'com.pghaz.revery.debug'
            ]

            buildConfigField "String", "SPOTIFY_CLIENT_ID", "\"7cb7f4b08ff748cc9f19c84cc627f9d9\""
            buildConfigField "String", "SPOTIFY_REDIRECT_URI", "\"com.pghaz.revery.debug://connect\""

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        beta {
            initWith debug

            applicationIdSuffix ".beta"
            debuggable false

            // Make sure this is consistent with the redirect URI scheme used in debug/strings.xml
            manifestPlaceholders = [
                    'appAuthRedirectScheme': 'com.pghaz.revery.beta'
            ]

            buildConfigField "String", "SPOTIFY_CLIENT_ID", "\"7cb7f4b08ff748cc9f19c84cc627f9d9\""
            buildConfigField "String", "SPOTIFY_REDIRECT_URI", "\"com.pghaz.revery.beta://connect\""

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }

        release {
            debuggable false

            // Make sure this is consistent with the redirect URI scheme in release/strings.xml
            manifestPlaceholders = [
                    'appAuthRedirectScheme': 'com.pghaz.revery'
            ]

            buildConfigField "String", "SPOTIFY_CLIENT_ID", "\"7cb7f4b08ff748cc9f19c84cc627f9d9\""
            buildConfigField "String", "SPOTIFY_REDIRECT_URI", "\"com.pghaz.revery://connect\""

            signingConfig signingConfigs.release_signing_config

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }

    // This is necessary to add so that Spotify Auth lib works properly
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude "DebugProbesKt.bin"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.0-M1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0-M1'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-beta01"

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.shawnlin:number-picker:2.4.10'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'jp.wasabeef:picasso-transformations:2.4.0'
    implementation "com.ncorti:slidetoact:0.9.0"

    // Spotify SDK App Remote
    implementation project(':spotify-app-remote')
    implementation "com.google.code.gson:gson:2.8.5"

    // Spotify Web API library
    implementation 'com.github.pghazal.spotify-web-api-android:api-retrofit2:1.0.2'
    // Spotify Authentication
    implementation 'com.github.pghazal.spotify-web-api-android:api-auth:1.0.2'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:25.12.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}